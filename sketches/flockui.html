<html>
  <head>
    <title>Flock UI Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script src='https://cdn.firebase.com/v0/firebase.js'></script>
    <script src="../lib/fbui.js"></script>
    
    <script type="text/coffeescript">
    
    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      # Firebase url and JSON tree as class variables.
      uiUrl: "https://agentscript.firebaseio.com/flock/ui"
      uiData:
        reset:          type:"button", setter:"reset",  val: false
        go:             type:"button", setter:"toggle", val: false
        step:           type:"button", setter:"once",   val: false
        background:     type:"choice", vals: ["black","red","random"], val: "random"
        useConsole:     type:"switch", val: true
        population:     type:"slider", min:25,  step:25,  max:1000, val:500
        minSeparation:  type:"slider", min:.25, step:.25, max:5,    val:.75
        name:           type:"input", val:""
        log:            type:"output", val:"" 
        vision:
         type:"slider", setter:"setVision", min:.5, step:.5, max:10, val:3
        maxTurn:
          type:"slider", setter:"setMaxTurn", min:.5, step:.5, max:20, val:3

      # UI methods not already in class Model
      setVision: (@vision) -> @patches.cacheRect @vision, true
      setMaxTurn: (@maxTurn) -> @maxTurn = u.degToRad(@maxTurn)
      toggle: -> if @anim.stopped then @start() else @stop()

      # One time setup of Firebase connection.
      startup: -> @ui = new ABM.FirebaseUI @uiUrl, @, @uiData
      setup: ->
        # u.randomSeed() # use for repeatable runs
        
        @refreshPatches = false # <3fps->4fps agents larger part of drawing 
        # No optimizations: 4fps
        # @patches.usePixels() # 4fps same as refresh off
        @patches.cacheAgentsHere() # 17fps, big help for inRadius
        # w/ refresh off and two optimizations, 22-23fps
        @agents.setUseSprites() # 24 -> 36
        
        @anim.setRate 60, false
      
        # Managed by UI:
        # @useConsole = true
        # @population = 500     # agents
        # @setVision 3          # patches & set patch rect
        # @minSeparation = 0.75 # patches
        # @setMaxTurn(3.0)      # degrees -> radians

        # defaults
        @agents.setDefault "size", 1.5
        # @agents.setDefault "color", [255,0,0] # slightly faster! ->38/39 ??
        
        switch @background
          when "random" then p.color = u.randomGray 0, 100 for p in @patches
          when "black" then @patches.setDefault "color", [0,0,0]
          when "red" then @patches.setDefault "color", [100,0,0]        
        
        # @agents.create @population, (a) => # fat arrow for @patches
        #   #a.size = 1.5
        #   a.setXY @patches.randomPt()...
        #   a.color = u.randomMapColor() # u.randomColor() # u.randomBrightColor()
        p.sprout 1 for p in @patches.nOf(@population)
        a.color = u.randomMapColor() for a in @agents
        @once() # run one step
        console.log "UI JSON Tree: ", @uiData
        console.log "Mange Firebase UI at #{@uiUrl}"

      step: ->
        if @useConsole and @anim.ticks % 25 is 0
          vec=@reportFlockVectorSize()
          console.log @anim.toString(), "flock alignment: #{vec}" if @useConsole
          @ui.setUIValue "log", {alignment: vec, step: @anim.ticks}, true
        @flock(a) for a in @agents
        if @anim.draws is 2 # Show the sprite sheet if there is one after first draw
          sheet = u.last(u.s.spriteSheets) if u.s.spriteSheets.length isnt 0
          if sheet?
            console.log sheet if @useConsole
            document.getElementById("play").appendChild(sheet.canvas)

      flock: (a) -> # a is agent
        flockmates = @agents.inRadius a, @vision
        if flockmates.length isnt 0
          [nearestNeighbor, d] = u.minOneOf flockmates, ((f) -> f.distance a), true
          if d < @minSeparation
            @separate a, nearestNeighbor
          else
            @align a, flockmates
            @cohere a, flockmates
        a.forward .5 # move partial patch for smooth animation.

      separate: (a, nearest) ->
        h = nearest.towards a; @turnTowards a, h
      align: (a, flockmates) ->
        @turnTowards a, @averageHeading(flockmates)
      cohere: (a, flockmates) ->
        @turnTowards a, @averageHeadingTowards(a, flockmates)
        
      turnTowards: (a, heading) ->
        turn = u.subtractRads heading, a.heading # angle from h to a
        turn = u.clamp turn, -@maxTurn, @maxTurn # limit the turn
        a.rotate turn
      averageHeading: (flockmates) ->
        dx = (Math.sin f.heading for f in flockmates).reduce (x,y) -> x+y
        dy = (Math.cos f.heading for f in flockmates).reduce (x,y) -> x+y
        @safeAtan dy, dx
      averageHeadingTowards: (a, flockmates) ->
        dx = (Math.sin f.towards a for f in flockmates).reduce (x,y) -> x+y
        dy = (Math.cos f.towards a for f in flockmates).reduce (x,y) -> x+y
        @safeAtan dy, dx
      safeAtan: (x,y) -> Math.atan2 y, x # if x is 0 and y is 0 then 0 else Math.atan2 y, x # atan ok if x 0
      
      reportFlockVectorSize: ->
        dx = (Math.sin a.heading for a in @agents).reduce (x,y) -> x+y
        dy = (Math.cos a.heading for a in @agents).reduce (x,y) -> x+y
        Math.sqrt(dx*dx + dy*dy)/@population

    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    # Don't start, let UI start/stop model
    model = new MyModel("layers", 15, -16, 16, -16, 16, true, false)
      .debug() # Debug: Put Model vars in global name space

    </script>
  </head>
  <body>
    <div id="layers"></div>
    <div id="play" style="float:right;"></div>
  </body>
</html>