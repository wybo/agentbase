<html>
  <head>
    <title>NBody Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      setup: ->
        @refreshPatches = false # for static patches
        
        # globals
        @population = 4
        @sun = true
        @v0max = 1
        @gravExp = -2
        
        # defaults
        @agents.setDefault "shape", "circle"
        @agents.setDefault "heading", 0 # override promotion to random angle

        # window.foo = @patches.nOf(@population)
        for p in @patches.nOf(@population)
          p.sprout 1, @agents, (a) =>
            if @sun and a.id is 0
              a.mass = 10000; a.setXY 0,0; a.vx = a.vy = 0
            else
              a.mass = 10 + u.randomInt(1000)
              a.vx = @v0max * (1 - u.randomFloat(2))
              a.vy = @v0max * (1 - u.randomFloat(2))
              # a.penDown = true
            a.size = 3 * u.log10(a.mass)

        # @agents.setDefaultPen(1, true)
        @agents.setDefault "penSize", 1 #Pen(1, true)
        @agents.setDefault "penDown", true #Pen(1, true)
      step: ->
        for a in @agents
          for a1 in @agents.other(a)
            k = Math.pow(10, @gravExp)*a1.mass/Math.max(Math.pow(a.distance(a1),3), .000001)
            a.vx += (a1.x-a.x)*k; a.vy += (a1.y-a.y)*k
        for a in @agents when not (@sun and a.id is 0)
          # unless (@sun and a.id is 0)
          a.setXY u.clamp(a.x+a.vx, @patches.minX, @patches.maxX),
                  u.clamp(a.y+a.vy, @patches.minY, @patches.maxY)
          a.vx = u.sign(-a.x)*.001 if Math.abs(a.x) is @patches.maxX
          a.vy = u.sign(-a.y)*.001 if Math.abs(a.y) is @patches.maxY
          # a.setXY 0,0 if @sun and a.id is 0
        null
        
    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    model = new MyModel {
      div: "layers",
      size: 2,
      minX: -120,
      maxX: 120,
      minY: -100,
      maxY: 100,
      hasNeighbors: false
    }
    model.debug() # Debug: Put Model vars in global name space
    model.start() # Run model immediately after startup initialization
    
    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>