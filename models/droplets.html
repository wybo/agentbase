<html>
  <head>
    <title>Droplets Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../lib/data.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">

    u = ABM.util; DataSet = ABM.DataSet # aliases

    class MyModel extends ABM.Model
      startup: ->
        console.log "startup: elevation"
        @elevation = DataSet.importAscDataSet "data/nldroplets.asc", (ds)=>
          console.log "startup: creating slope/aspect data sets"
          # All work due to hybred array/object return
          # { slope: @slope, aspect: @aspect } = @elevation.slopeAndAspect()
          { @slope, @aspect } = @elevation.slopeAndAspect()
          # { @aspect, @slope } = @elevation.slopeAndAspect() # switched order fine
          # [ @slope, @aspect ] = @elevation.slopeAndAspect()
        
      setup: ->
        console.log "setup"
        @refreshPatches = @refreshLinks = false
        
        @patches.own "elevation slope aspect"
        # @globals "elevation slope aspect vision speed" # fix: problem with removeItem
        
        @vision = 1 # try range in 1-5 or so
        @speed = .25
        @useAspect = true # model uses aspect/slope or elevation.
        @lastMoved = @patches.length
        
        @agents.setDefault "shape", "square"
        @agents.setDefault "size", .5
        @agents.setDefault "color", [100,100,150]
        @patches.cacheRectangle @vision, false # cache inRadius @vision
        
        # Install startup globals into patches/drawing
        # img = @elevation.toDrawing()
        # @patches.installColors img
        # p.elevation = p.color[0] for p in @patches # could also use toPatchVar below
        # @slope.toPatchVar("slope")
        # @aspect.toPatchVar("aspect")
        @img=@elevation.toDrawing()
        @elevation.toPatchVar("elevation")
        @slope.toPatchVar("slope")
        @aspect.toPatchVar("aspect")
        
        p.sprout 1 for p in @patches.create()
        console.log "patches: #{@patches.length}, agents: #{@agents.length}"
      

      step: ->
        moved = 0
        for a in @agents
          if @useAspect
            neighbors = a.patch.neighbors
          else
            neighbors = a.patch.pRectangle
          neighbors = neighbors.minOneOf "elevation"
          if a.patch.elevation > neighbors.elevation #or a.patch.slope < .0001
            if @useAspect
              a.heading = a.patch.aspect
            else
              a.face neighbors
            a.forward @speed
            moved += 1
        if moved is 0 or (@useAspect and @lastMoved is moved)
          console.log "done, ticks: #{@animator.ticks}"
          @stop()
        if @animator.ticks % 100 is 0
          console.log @animator.toString(), "moved: #{moved}"
          @lastMoved = moved
    
    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors
    #   Defaults: 13, -16, 16, -16, 16, false, true
    model = new MyModel({
      div: "layers",
      size: 6,
      minX: 0,
      maxX: 80,
      minY: 0,
      maxY: 80
    })
    .debug() # Debug: Put Model vars in global name space
    .start() # Run model immediately after startup initialization
    
    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>
